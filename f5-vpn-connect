#!/usr/bin/env osascript
#
# Automate F5 BigIP Client by filling in username, password, and TOTP code
#
# Replace keyring command and TOTP command as needed.
# If using keyring command installed by python keyring module, then create
# the keyring entry with this command: `keyring set bigip_pass my_username`
#

on run username
	set keyring_cmd to "/usr/local/bin/keyring get bigip_pass " & username
	set totp_cmd to "~/bin/totp -p " & username
	set mypass to do shell script (keyring_cmd)

	tell application "BIG-IP Edge Client"
		activate
		tell application "System Events"
			tell process "BIG-IP Edge Client"
				tell window "BIG-IP Edge Client"
					click radio button "Connect" of radio group 1
					# Wait until the connect dialog is loaded
					repeat until exists group 3 of UI element 1 of scroll area 1 of group 1
						delay 0.2
					end repeat
					tell UI element 1 of scroll area 1 of group 1
						set value of text field 1 of group 3 to username
						set value of text field 1 of group 4 to mypass
						click UI element "Sign in" of group 5
						set totp to do shell script (totp_cmd)
						# Wait until the connect dialog is loaded
						repeat until exists text field 1 of group 2
							delay 0.2
						end repeat
						set value of text field 1 of group 2 to totp
						click UI element "Verify" of group 1 of group 6 of group 2
					end tell
				end tell
			end tell
		end tell
	end tell
	return
end run
